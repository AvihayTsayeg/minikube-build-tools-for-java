plugins {
  id 'java-gradle-plugin'
  id 'maven'
  id 'checkstyle'
  id 'com.github.sherter.google-java-format' version '0.6'
  id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.5'
  testCompile 'junit:junit:4.11'
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.0"
}

group = 'com.google.cloud.tools'
version = '1.0.0-SNAPSHOT'

jar {
  manifest {
    attributes 'Implementation-Title': project.name,
        'Implementation-Version': version,
        'Built-By': System.getProperty('user.name'),
        'Built-Date': new Date(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion
  }
}

test {
  testLogging {
    showStandardStreams = true
    exceptionFormat = 'full'
  }
}


/* GOOGLE JAVA FORMAT */
check.dependsOn verifyGoogleJavaFormat
/* GOOGLE JAVA FORMAT */


/* CHECKSTYLE */
checkstyle {
  toolVersion = "7.6.1"
  // get the google_checks.xml file from the actual tool we're invoking
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], 'google_checks.xml')
  maxErrors = 0
  maxWarnings = 0
}
/* CHECKSTYLE */

/* RELEASE */
task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

task writePom {
  project.afterEvaluate {
    def outputFile = file("$buildDir/pom/${project.name}-${project.version}.pom")
    outputs.file outputFile

    doLast {
      pom {
        project {
          name 'App Engine Gradle Plugin'
          description 'This Gradle plugin provides tasks to build and deploy Google App Engine applications.'
          url 'https://github.com/GoogleCloudPlatform/app-gradle-plugin'
          inceptionYear '2016'

          scm {
            url 'https://github.com/GoogleCloudPlatform/app-gradle-plugin'
            connection 'scm:https://github.com/GoogleCloudPlatform/app-gradle-plugin.git'
            developerConnection 'scm:git://github.com/GoogleCloudPlatform/app-gradle-plugin.git'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id 'loosebazooka'
              name 'Appu Goundan'
              email 'appu@google.com'
            }
          }
        }
      }.writeTo(outputFile)
    }
  }
}

// this is for kokoro sign and releases
task prepareRelease(type: Copy) {
  from jar
  from sourceJar
  from javadocJar
  from writePom

  into "${buildDir}/release-artifacts"

  dependsOn build
  dependsOn cleanPrepareRelease
}

// Release plugin (git release commits and version updates)
release {
  tagTemplate = 'v$version'
  git {
    requireBranch = /^release_v\d+.*$/  //regex
  }
}
/* RELEASE */
